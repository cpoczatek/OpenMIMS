// "StartupMacros"

   // Global variables
   var pencilWidth=1, eraserWidth=10, leftClick=16, alt=8;
   var brushWidth = 10;
   var floodType = "8-connected"; 

   //macro "AutoRun" {print('AutoRun');}

  macro "OpenMIMS tool - Ca00L07f7L707fL0729L0725Lf7d5Lf7d9L7092L7052L7f5dL7f9d" {
      //empty tool trickery!
 }

   macro "Drag To Writer tool - CfffL00b0CeeeLc0d0CfffLe0f0L0111CbbbD21C777D31C888L41a1C555Db1C688Dc1C677Dd1CdddDe1CfffDf1L0212C888D22CfffL3292CaaaDa2C689Db2C7deDc2C6cdDd2C477De2CdddDf2CfffD03CeeeD13C999D23CdddD33CcccL4353CeeeD63CfffL7383CaaaD93C689Da3C7deDb3C6ceDc3C1acDd3C378De3CdddDf3CfffD04CeeeD14C999D24CdddD34CcccL4454CeeeD64CfffD74CaaaD84C689D94C7deDa4C6ceDb4C1adDc4C378Dd4CbbbDe4CfffDf4D05CeeeD15C999D25CdddD35CcccL4555CfffD65CaaaD75C689D85C7deD95C6ceDa5C1acDb5C378Dc5CaaaDd5CfffLe5f5D06CeeeD16C999D26CfffL3656CaaaD66C689D76C7deD86C6ceD96C1acDa6C378Db6C666Dc6CdddDd6CfffLe6f6D07CeeeD17C999D27CdddD37CcccD47C888D57C689D67C7deD77C6ceD87C1acD97C378Da7C999Lb7c7CdddDd7CfffLe7f7D08CeeeD18C999D28CfffD38CdddD48C785D58C7ddD68C6ceD78C1adD88C378D98CbbbDa8CdddDb8C999Dc8CdddDd8CfffLe8f8D09CeeeD19C999D29CeeeD39CaaaD49Cbb3D59Cdf4D69C2bcD79C378D89C999D99CeeeDa9CdddDb9C999Dc9CdddDd9CfffLe9f9D0aCeeeD1aC999D2aCfffD3aC999D4aCbb3D5aCbc3D6aC785D7aCaaaD8aCeeeD9aCfffDaaCdddDbaC999DcaCdddDdaCfffLeafaD0bCeeeD1bC999D2bCfffD3bCdddD4bC888D5bCaaaD6bCcccD7bCeeeL8b9bCfffDabCdddDbbC999DcbCdddDdbCfffLebfbD0cCeeeD1cC999D2cCeeeD3cCcccL4c9cCeeeDacCdddDbcC999DccCdddDdcCfffLecfcL0d1dC888D2dCfffL3dadCdddDbdC888DcdCeeeDddCfffLedfdL0e1eCbbbD2eC777D3eC888L4eaeC777DbeCaaaDceCfffLdefeL0fff" {}
   macro "Unused Tool -" {}

   macro "Pencil Tool - C037L494fL4990L90b0Lc1c3L82a4Lb58bL7c4fDb4L5a5dL6b6cD7b" {
        getCursorLoc(x, y, z, flags);
        if (flags&alt!=0)
              setColorToBackgound();
        draw(pencilWidth);
   }

   macro "Paintbrush Tool - C037La077Ld098L6859L4a2fL2f4fL3f99L5e9bL9b98L6888L5e8dL888c" {
        getCursorLoc(x, y, z, flags);
        if (flags&alt!=0)
              setColorToBackgound();
        draw(brushWidth);
   }

    macro "Flood Fill Tool -C037B21P085373b75d0L4d1aL3135L4050L6166D57D77D68La5adLb6bcD09D94" {
        setupUndo();
        getCursorLoc(x, y, z, flags);
        if (flags&alt!=0) setColorToBackgound();
        floodFill(x, y, floodType);
     }


    macro "About ImageJ... Action Tool - C037T3e16?" {
        run("About ImageJ...");
    }

   function draw(width) {
        setupUndo();
        getCursorLoc(x, y, z, flags);
        setLineWidth(width);
        moveTo(x,y);
        x2=-1; y2=-1;
        while (true) {
            getCursorLoc(x, y, z, flags);
            if (flags&leftClick==0) exit();
            if (x!=x2 || y!=y2)
                lineTo(x,y);
            x2=x; y2 =y;
            wait(10);
        }
   }

   function setColorToBackgound() {
       savep = getPixel(0, 0);
       makeRectangle(0, 0, 1, 1);
       run("Clear");
       background = getPixel(0, 0);
       run("Select None");
       setPixel(0, 0, savep);
       setColor(background);
   }

  macro 'Pencil Tool Options...' {
       pencilWidth = getNumber("Pencil Width (pixels):", pencilWidth);
  }

  macro 'Paintbrush Tool Options...' {
      brushWidth = getNumber("Brush Width (pixels):", brushWidth);
   }

  macro 'Flood Fill Tool Options...' {
      Dialog.create("Flood Fill Tool");
      Dialog.addChoice("Flood Type:", newArray("4-connected", "8-connected"), floodType);
      Dialog.show();
      floodType = Dialog.getChoice();
  }


  



